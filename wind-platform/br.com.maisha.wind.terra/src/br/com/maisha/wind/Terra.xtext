grammar br.com.maisha.wind.Terra with org.eclipse.xtext.common.Terminals

generate terra "http://www.wind-platform.com.br/maisha/wind/Terra"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

DomainObject:
	package = PackageDeclaration 
	imports += Import* 
	'domain_object' name=ValidID label=STRING '{'
	 & properties+=ObjectProperty* 
	 & attributes+=Attribute* 
	 & operations+=Operation* 
	 & validations+=Validation* 
	'}';
	
Import:
	'import' importedNamespace=QualifiedName;

PackageDeclaration:
	'package' name=QualifiedName ';';

ObjectProperty:
	name=ObjectPropertyName ':' value=Value;

Attribute:
	type=ValidID ref=ValidID label=STRING '{'
	properties+=AttributeProperty*
	'}';

AttributeProperty:
	name=AttributePropertyName ':'  (validValues=Array | value=Value |  value=Value 'using' validValues = Array );

Operation:
	'operation' type=OperationType name=ValidID label=STRING '{'
	properties+=OperationProperty*
	'}';

OperationProperty:
	name=OperationPropertyName ':' (validValues=Array | value=Value );


Value:
	'true' | 'false' | STRING | ValidID | EXPRESSION | NUMBER | PATH;

Validation: 'validation' ref=ID  '{' rules += ValidationRule* '}';

Array:
	'[' {Array} values+= STRING (',' STRING)* ']'
;

ValidationRule:
	messageKey=STRING ':' expression=EXPRESSION;

terminal EXPRESSION:
	'${' ('a'..'z'|'A'..'Z'|'$'|'_'|'*'|'+'|'-'|'='|'/'|'0'..'9'| ' ' | '?' | ':' | '(' | ')' | '<' | '>' | '.')* '}';

terminal PATH:
	ID ('/' ID)+ '.' ID
;

terminal NUMBER returns ecore::EBigDecimal:
  ('0'..'9')* ('.' ('0'..'9')+)?;


terminal INT returns ecore::EInt:
  "$$$don't use this anymore$$$";

QualifiedName:
  ValidID ('.' ValidID)*;
  
ValidID:
	ID
;

terminal ID:
	'^'? ('a'..'z'|'A'..'Z'|'$'|'_') ('a'..'z'|'A'..'Z'|'$'|'_'|'0'..'9')*;

enum OperationType:
	groovy;

enum PresentationType:
	text | radio | checkbox | combo | list | textarea | date | related | embedded_object | group | folder;

enum AttributePropertyName:
	x | y | colspan | rowspan | disabled | icon | width | height | tooltip |
	group | visibleInEdition | visibleInGrid | content | value | validValues | validation | required | max_length |
	min_length | range | mask | event | toString | onetomany | manytoone | transient | folder | presentation_type | sequence;

enum OperationPropertyName:
	disabled | icon | tooltip | visible | file | validWhen | is_filter | validate;

enum ObjectPropertyName:
	open_filtering | event_handler;

		
